plugins {
    id 'groovy'
    id 'java-test-fixtures'
    id 'cpp'
    id 'gradlebuild.jni'
    id 'gradlebuild.freebsd'
    id 'gradlebuild.ncurses'
}

nativeVersion {
    versionClassPackageName = "net.rubygrapefruit.platform.internal.jni"
    versionClassName = "NativeVersion"
}

dependencies {
    implementation gradleApi()
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
}

javadoc {
    exclude '**/internal/**'
}

model {
    components {
        nativePlatform(NativeLibrarySpec) {
            baseName 'native-platform'
            $.platforms.each { p ->
                if (p.name.contains("ncurses")) {
                    return
                }
                targetPlatform p.name
            }
            sources {
                cpp {
                    source.srcDirs = ['src/shared/cpp', 'src/main/cpp']
                    exportedHeaders.srcDirs = ['src/shared/headers']
                }
            }
        }

        nativePlatformCurses(NativeLibrarySpec) {
            baseName 'native-platform-curses'
            $.platforms.each { p ->
                if (p.operatingSystem.windows) {
                    return
                }
                if (p.operatingSystem.linux && !p.name.contains("ncurses")) {
                    return
                }
                targetPlatform p.name
            }
            sources {
                cpp {
                    source.srcDirs = ['src/shared/cpp', 'src/curses/cpp']
                    exportedHeaders.srcDirs = ['src/shared/headers']
                }
            }
        }
    }
}
