plugins {
    id('application')
    id('gradlebuild.native-platform-component')
}

application {
    mainClass = 'net.rubygrapefruit.platform.test.Main'
    applicationName = 'native-platform-test'
}

base {
    archivesName = 'native-platform-test'
}

if (versions.useRepo) {
    configurations.configureEach {
        resolutionStrategy.dependencySubstitution {
            substitute project(':native-platform') using module("net.rubygrapefruit:native-platform:${version}")
        }
    }
    configurations {
        download
    }
    dependencies {
        download "$group:${archivesBaseName}:${version}"
    }
}

dependencies {
    implementation project(':native-platform')
    implementation 'net.sf.jopt-simple:jopt-simple:4.2'
}

publishing {
    publications {
        main(MavenPublication) {
            artifact(distZip)
        }
    }
}

// Download and unpack the application to allow it to be tested
tasks.register('download') {
    def downloadDir = project.layout.buildDirectory.dir("download")
    doLast {
        copy {
            from(configurations.download.files.collect { zipTree(it) })
            into(downloadDir)
        }
    }
}

// Use System.in as input stream for the launched process to enable interactive input
tasks.named("run", JavaExec) {
    standardInput = System.in
}

java {
    toolchain {
        // Configure Java 8, so we test that native-platform is compiled to Java 8
        languageVersion = JavaLanguageVersion.of(8)
    }
}
