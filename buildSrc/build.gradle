plugins {
    id('java-gradle-plugin')
}

repositories {
    jcenter()
    gradlePluginPortal()
    maven { url = 'https://repo.nokee.dev/snapshot' }
}

dependencies {
    implementation gradleApi()
    implementation platform('dev.nokee:nokee-gradle-plugins:0.4.1570-202202221519.38e1fe69')
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.0.1'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.gradle:test-retry-gradle-plugin:1.1.8'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

if (JavaVersion.current() < JavaVersion.VERSION_1_8) {
    // Need to use Java 8 on some older platforms
    throw new UnsupportedOperationException("At least Java 8 is required to build native-platform. Earlier versions are not supported.")
}

gradlePlugin {
    plugins {
        jniNokee {
            id = "gradlebuild.jni"
            implementationClass = "gradlebuild.JniPlugin"
        }
        nativeComponent {
            id = "gradlebuild.native-platform-component"
            implementationClass = "gradlebuild.NativePlatformComponentPlugin"
        }
        freebsdRuntime {
            id = "gradlebuild.freebsd-runtime"
            implementationClass = "gradlebuild.FreeBsdRuntimePlugin"
        }
        ncursesRuntime {
            id = "gradlebuild.ncurses-runtime"
            implementationClass = "gradlebuild.NcursesRuntimePlugin"
        }
    }
}
